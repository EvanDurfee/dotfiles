do_zprofile=false # profile zsh startup
if $do_zprofile; then zmodload zsh/zprof; fi
if [ -z "$DOTFILES_PROFILE_LOADED" ]; then echo "profile missing, exiting!" >&2; return 1; fi

################################################################################
# P10K Instant Prompt
################################################################################
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
	source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

################################################################################
# General Options
################################################################################
# setopt correct                                                  # Auto correct mistakes
setopt extendedglob                                             # Extended globbing. Allows using regular expressions with *
setopt interactivecomments                                      # Interpet # as a comment in interactive shells
# setopt nocaseglob                                               # Case insensitive globbing
# setopt nomatch                                                  # return a non-zero status if no results match the pattern
# setopt completealiases                                          # disables!? autocompletion for aliases
setopt rcexpandparam                                            # Array expansion with parameters
setopt nocheckjobs                                              # Don't warn about running processes when exiting
setopt numericglobsort                                          # Sort filenames numerically when it makes sense
setopt nobeep                                                   # No beep
setopt complete_in_word                                         # Allow completion from within a word or phrase

WORDCHARS=${WORDCHARS//[\/;]}                                   # Treat the characters / and ; as word separators
autoload -U select-word-style
select-word-style normal                                        # Set to "bash" to ignore WORDCHARS and use only alphanumerics

################################################################################
# History
################################################################################
# setopt append_history                                           # append history instead of overwriting
setopt extended_history                                         # save timestamp and duration to history
# setopt inc_append_history                                       # save commands are added to the history immediately, otherwise only when shell exits.
setopt inc_append_history_time                                  # Like inc_append_history, but saves after the command completes so that execution time is correct with extended history
setopt hist_reduce_blanks                                       # remove superfluous blanks
# setopt hist_ignore_dups                                         # Don't add to history if this command is the same as the previous
# setopt hist_find_no_dups                                        # When searching back through history, skip duplicates
setopt hist_ignore_all_dups                                     # If a new command is a duplicate, remove the older one

HISTSIZE=200000
SAVEHIST=$HISTSIZE
HISTFILE="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/history"
[ ! -f "$HISTFILE" ] && mkdir -p "$HISTFILE:h" && touch "$HISTFILE" && chmod 600 "$HISTFILE"

################################################################################
# cd/pushd
################################################################################
setopt auto_cd                                                  # if only directory path is entered, cd there.
# Sets cd to behave like pushd (allows cd -[n] or cd +[n] to navigate the stack)
# Use cd -<TAB> or cd +<TAB> to show the stack
setopt auto_pushd                                               # Make cd push the old directory onto the dir stack
setopt pushd_silent                                             # Do not print the directory stack after pushd or popd
setopt pushd_to_home                                            # pushd with no args acts like pushd $HOME
setopt pushd_ignore_dups                                        # Don't push multiple copies of the same dir
setopt pushd_minus                                              # Swap meaning of + and - (- will index from the top of the stack, + from the bottom)
DIRSTACKSIZE=20

################################################################################
# Keybindings
################################################################################
bindkey -e
bindkey '^[[7~' beginning-of-line                               # Home key
bindkey '^[[H' beginning-of-line                                # Home key
if [[ "${terminfo[khome]}" != "" ]]; then
	bindkey "${terminfo[khome]}" beginning-of-line                # [Home] - Go to beginning of line
fi
bindkey '^[[8~' end-of-line                                     # End key
bindkey '^[[F' end-of-line                                     # End key
if [[ "${terminfo[kend]}" != "" ]]; then
	bindkey "${terminfo[kend]}" end-of-line                       # [End] - Go to end of line
fi
bindkey '^[[2~' overwrite-mode                                  # Insert key
bindkey '^[[3~' delete-char                                     # Delete key
bindkey '^?' backward-delete-char                               # Backspace key

bindkey '^[[C'  forward-char                                    # Right key
bindkey '^[[D'  backward-char                                   # Left key
bindkey '^[OA' up-line-or-history                               # Up
bindkey '^[OB' down-line-or-history                             # Down

bindkey '^[[5~' history-beginning-search-backward               # Page up key
bindkey '^[[6~' history-beginning-search-forward                # Page down key

# These modules are needed for these key bindings
zmodload zsh/complist
zmodload zsh/terminfo
bindkey -M menuselect $terminfo[kpp] backward-word              # Page up in menus
bindkey -M menuselect $terminfo[knp] forward-word               # Pade down in menus

# Navigate words with ctrl+arrow keys
bindkey '^[Oc' forward-word                                     # Ctrl+right
bindkey '^[Od' backward-word                                    # Ctrl+left
bindkey '^[[1;5D' backward-word                                 # Ctrl+left
bindkey '^[[1;5C' forward-word                                  # Ctrl+right

bindkey '^[[3;5~' kill-word                                     # delete next word with ctrl+delete
bindkey '^H' backward-kill-word                                 # delete previous word with ctrl+backspace
# bindkey '^[[Z' undo                                             # Shift+tab undo last action
bindkey '^[[Z' reverse-menu-complete                            # Shift+tab to jump to the previous completiong

################################################################################
# Colors
################################################################################
autoload -U colors && colors

# Prepare the LS_COLORS variable (clear the cache if changing the config)
eval "$(dircolors -b "$XDG_CONFIG_HOME/dircolors/colors.conf")"

################################################################################
# Homebrew
################################################################################

# If homebrew isn't already loaded, load it now
if [ -z "$HOMEBREW_PREFIX" ] && [ -e /home/linuxbrew/.linuxbrew/bin/brew ]; then
	eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi

################################################################################
# Plugins
################################################################################
ezplug () {
	# Simple plugin manager functionality
	local plugin_dir
	# TODO: Create casks in own tap to support this
	if [ -e "$HOMEBREW_PREFIX/share/${1:t}/${1:t}.plugin.zsh" ] \
			|| [ -e "$HOMEBREW_PREFIX/share/${1:t}/${1:t}.zsh-theme" ]; then
		# Check if the plugin is installed from homebrew
		plugin_dir="$HOMEBREW_PREFIX/share/${1:t}"
	else
		local plugin_name_hash state_dir update_file
		# Git clone the plugin
		plugin_name_hash="$(echo "$1" | sha256sum | cut -d ' ' -f1)"
		state_dir="${XDG_STATE_HOME:-$HOME/.local/state}"/zsh/plugins/updates
		update_file="$state_dir"/"$plugin_name_hash"
		plugin_dir="${XDG_DATA_HOME:-$HOME/.local/share}"/zsh/plugins/"$1"

		mkdir -p "$state_dir"

		# clone plugin if not present
		if [ ! -e "$plugin_dir" ]; then
			mkdir -p "$plugin_dir"
			git clone --depth=1 "https://github.com/$1.git" "$plugin_dir" && touch "$state_dir"/"$plugin_name_hash"
		fi
		# Update plugin if not updated recently
		if ! [ -e "$state_dir"/"$plugin_name_hash" ] || (( "$(date -r "$update_file" +%s)" < ("$(date +%s)" - 60 * 60 * 24 * 7) )); then
			git -C "$plugin_dir" pull >/dev/null && touch "$state_dir"/"$plugin_name_hash"
		fi
	fi
	if [ -e "$plugin_dir"/"${1:t}.plugin.zsh" ]; then
		source "$plugin_dir"/"${1:t}.plugin.zsh"
	fi
	if [ -e "$plugin_dir"/"${1:t}.zsh-theme" ]; then
		source "$plugin_dir"/"${1:t}.zsh-theme"
	fi
}

# -- P10K Prompt --
ezplug romkatv/powerlevel10k
source "$ZDOTDIR/.p10k.zsh"

# -- Additional Completions --
ezplug zsh-users/zsh-completions

# -- In-line completion suggestion --
ezplug zsh-users/zsh-autosuggestions
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=8"
ZSH_AUTOSUGGEST_STRATEGY=(history completion)  # autosuggest history first, then completions

# -- Automated scripts on dir enter / exit --
ezplug Tarrasch/zsh-autoenv

# Adds the ability to customize terminal colors 0-15 (or 0-21 on a 256 color terminal)
# Mainly for configuring terminal colors via script instead of using the terminal emulator profile
# Sticking to ptyxis/kitty for easier freedeskotp color scheme integration
# ezplug chriskempson/base16-shell

# Use syntax highlighting (Note: wants to be after anything that modifies the line buffer)
ezplug zsh-users/zsh-syntax-highlighting
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
# ezplug zdharma-continuum/fast-syntax-highlighting


# History substring search (must come after syntax highligting)
# This searches history for any lines containing the string, not just starting with it
# Disabled for now in favor of regular up/down history search and fzf history
# ezplug zsh-users/zsh-history-substring-search

# bind UP and DOWN arrow keys to history substring search
# bindkey '^[[A' history-substring-search-up
# bindkey '^[OA' history-substring-search-up
# bindkey '^[[B' history-substring-search-down
# bindkey '^[OB' history-substring-search-down

################################################################################
# FZF integration
################################################################################
source <(fzf --zsh)
# Customizes history search fzf flags
# --preview... shows a preview window with the output of the given command ({2..} skips the first field of the input, the histroy number)
# --preview-window moves the window to the top, wraps text, and sizes to max 3 lines
# --bind keys to various actions
export FZF_CTRL_R_OPTS="\
  --preview 'echo -n {2..} | bat --color=always --language=zsh --plain --strip-ansi=always'
  --preview-window up:wrap:3
  --bind 'ctrl-/:toggle-preview'
  --bind 'ctrl-k:print-query+abort'
  --bind 'ctrl-y:execute-silent(echo -n {2..} | clip)+abort'
  --bind 'enter:accept-or-print-query'
  --bind 'esc:abort'
  --color header:italic
  --header 'Press CTRL-Y to copy command into clipboard, CTRL-K to keep prompt'"


################################################################################
# Other fpath and shell integrations
################################################################################
# Add functions dir to fpath and load them
fpath=("${ZDOTDIR:-$HOME}/functions" "${fpath[@]}")
autoload -Uz $fpath[1]/*(.:t)

# Add ubuntu completions path
# docker is hardcoded to use this even on fedora
if [ -d /usr/share/zsh/vendor-completions ]; then
	fpath=("${fpath[@]}" /usr/share/zsh/vendor-completions)
fi

# Load completions if installed
if flatpak info com.bitwarden.desktop >/dev/null 2>&1; then
	source <(flatpak run --command=bw com.bitwarden.desktop completion --shell zsh)
fi
if hash kubectl 2>/dev/null; then
	source <(kubectl completion zsh)
fi
if hash helm 2>/dev/null; then
	source <(helm completion zsh)
fi
if hash rustup 2>/dev/null; then
	source <(rustup completions zsh)
	source <(rustup completions zsh cargo)
fi

################################################################################
# Completions
################################################################################
zstyle ':completion:*' completer _complete                      # Basic completions; _approximate can be unpredictable
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'm:{A-Z}={a-z}'  # Case insensitive tab completion
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"         # Colored completion (different colors for dirs/files/etc)
zstyle ':completion:*' rehash true                              # automatically find new executables in path
zstyle ':completion:*' squeeze-slashes true                     # Treat // as / in file paths instead of /*/
zstyle ':completion:*' accept-exact '*(N)'                      # Auto-accept if there is only one option
zstyle ':completion:*' menu select                              # enable menu style completions
# Speed up completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME:-"$HOME"/.cache}"/zsh/zcompcache
# Display groups
# zstyle ':completion:*:descriptions' format '%F{green}-- %d --%f'  # Set group formatting (otherwise groups are hidden)
# zstyle ':completion:*' group-name ''                            # Use group format as separators (instead of just at top)

################################################################################
# FZF-tab completions plugin
################################################################################
ezplug Aloxaf/fzf-tab
zstyle ':completion:*' sort false                           # Disable fzf completion sorting
zstyle ':completion:*:descriptions' format '[%d]'           # Set description format to enable fzf-tab group support (auto-colored)
zstyle ':completion:*' menu no                              # Menu style is incompatible with fzf-tab :(
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'  # Preview directory completions
# --header breaks the tab completion and cannot be used
# case insensitive matching
# ctrl-space: select multiple
# ctrl-k: print query string instead of using a suggestion
# enter: print query string if no suggestions, else print chosen suggestion(s)
zstyle ':fzf-tab:*' fzf-flags \
	-i \
	--bind 'ctrl-k:print-query+abort' \
	--bind 'enter:accept-or-print-query'

# Add custom fzf flags
zstyle ':fzf-tab:*' switch-group '<' '>'                    # Set keys to switch between completion groups

# Load the completion system
autoload -Uz compinit && compinit

################################################################################
# Zoxide
################################################################################
# Zoxide hooks must run after compinit (requires compdef)
# Adds smart search for ambiguous cd
# Also can do 'cd partial <tab>' to trigger fuzzy completion
# Adds 'cdi' for interactive cd
if hash zoxide 2>/dev/null; then
	eval "$(zoxide init --cmd cd zsh)"
fi

################################################################################
# Misc
################################################################################

## Load aliases
if [ -f "${ZDOTDIR:-$HOME}/.zaliases" ]; then
	source "${ZDOTDIR:-$HOME}/.zaliases"
fi

## Load overrides
if [ -f "${ZDOTDIR:-$HOME}/.zoverride" ]; then
	source "${ZDOTDIR:-$HOME}/.zoverride"
fi

## Set terminal window and tab/icon title + pwd
# source "${ZDOTDIR:-$HOME}/set_term_info.zsh"

# Add named directory zsh
hash -d zsh="$ZDOTDIR"

# Use Bitwarden SSH agent
if [ -e "$HOME"/.var/app/com.bitwarden.desktop/data/.bitwarden-ssh-agent.sock ]; then
	export SSH_AUTH_SOCK="$HOME"/.var/app/com.bitwarden.desktop/data/.bitwarden-ssh-agent.sock
fi

# If running kitty terminal, add integration
if test -n "$KITTY_INSTALLATION_DIR"; then
    export KITTY_SHELL_INTEGRATION="no-cursor"
    autoload -Uz -- "$KITTY_INSTALLATION_DIR"/shell-integration/zsh/kitty-integration
    kitty-integration
    unfunction kitty-integration
fi

# Tmux stuff
if [ -n "$TMUX" ]; then
	# Tmux Smart Session Manager
	if [ -d "$XDG_DATA_HOME/tmux-plugins/t-smart-tmux-session-manager/bin" ]; then
		export PATH="$PATH:$XDG_DATA_HOME/tmux-plugins/t-smart-tmux-session-manager/bin"
	fi

	# Add inherent scroll support to less / bat as desired
	# Means less will handle scrolling instead of the terminal emulator, which is
	#  required for scrolling in tmux to work (only works for graphical sessions).
	# However, if less takes control of the cursor, it is no longer possible to
	#  highlight and copy text from the terminal emulator.
	if [ "$(less --version | head --lines=1 | grep --only-matching -E "[0-9]+")" -ge 549 ]; then
	#    export LESS="-RF --mouse --wheel-lines=5"
		export BAT_PAGER="less -RF --mouse --wheel-lines=5"
		export DELTA_PAGER="$BAT_PAGER"
	fi
fi

# Dump profiling results if enabled
if $do_zprofile; then zprof; fi
unset do_zprofile
